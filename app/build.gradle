plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

//主要版本
ext.versionMajor = 1
//次要版本(0-99)
ext.versionMinor = 0
//补丁版本(0-99)
ext.versionPatch = 0

android {
    signingConfigs {
        key {
            storeFile file('/Users/wanhui/AndroidStudioProjects/MvvmProject/mvvm.jks')
            storePassword 'wh2023'
            keyPassword 'wh2023'
            keyAlias 'mvvm'
        }
    }
    compileSdk 31

    defaultConfig {
        applicationId "com.wh.mvvmproject"
        minSdk 21
        targetSdk 31
        versionCode computeVersionCode()
        versionName computeVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    NETWORK_SECURITY_CONFIG: "@xml/network_security_config_debug"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.key

            manifestPlaceholders = [
                    NETWORK_SECURITY_CONFIG: "@xml/network_security_config"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    // 这个配置指定了一个应用 bundle 该如何根据
    // 语言、屏幕像素密度以及 cpu 架构 (abi) 来分拆 apk。
    // 默认值为 true
    // 这意味着每个不同的相关配置会生成一个分解后的 apk。
    bundle {
        language {
            enableSplit = false //app内置语言切换功能
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }
}

dependencies {
    //androidx
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //Android KTX : https://developer.android.google.cn/kotlin/ktx?hl=zh-cn
    implementation 'androidx.activity:activity-ktx:1.5.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'

    //google
    implementation 'com.google.android.material:material:1.4.0'

    //Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // 网络
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    //第三方

    //BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.11'

}

def computeVersionCode() {
    // 主要版本 + 次要版本 + 补丁版本
    return (versionMajor * 10000) + (versionMinor * 100) + versionPatch
}

def computeVersionName() {
    return "${versionMajor}.${String.format("%02d", versionMinor)}.${String.format("%02d", versionPatch)}"
}